scp_program -> proc_of_searching_for_current_state_of_specified_object
	(*
	-> rrel_params: .proc_of_searching_for_current_state_of_specified_object_params 
		(*
		-> rrel_1: rrel_in: _parameter;;
		-> rrel_2: rrel_in: _answer_proc_of_searching_for_current_state_of_specified_object;;
		*);;

	-> rrel_operators: .proc_of_searching_for_current_state_of_specified_object_operator_set 
		(*
		-> rrel_init: .proc_of_searching_for_current_state_of_specified_object_operator1
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_struct: _node;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _parameter;;

			-> rrel_set_1: rrel_assign: rrel_scp_var: _set;;

			=> nrel_then: .proc_of_searching_for_current_state_of_specified_object_operator2A;;
			=> nrel_else: .proc_of_searching_for_current_state_of_specified_object_operator_return;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator2A
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _node;;

			=> nrel_then: .proc_of_searching_for_current_state_of_specified_object_operator2B;;
			=> nrel_else: .proc_of_searching_for_current_state_of_specified_object_operator4;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator2B
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: rrel_pos_const_perm: _arc1;;

			=> nrel_goto: .proc_of_searching_for_current_state_of_specified_object_operator2C;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator2C
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: situation;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _node;;

			=> nrel_then: .proc_of_searching_for_current_state_of_specified_object_operator2D;;
			=> nrel_else: .proc_of_searching_for_current_state_of_specified_object_operator2A;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator2D
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: real_entity;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _node;;

			=> nrel_then: .proc_of_searching_for_current_state_of_specified_object_operator3;;
			=> nrel_else: .proc_of_searching_for_current_state_of_specified_object_operator2A;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator3
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _node;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _node2;;

			-> rrel_set_3: rrel_fixed: rrel_scp_var: _answer_proc_of_searching_for_current_state_of_specified_object;;

			=> nrel_goto: .proc_of_searching_for_current_state_of_specified_object_operator2A;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator4
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set;;

			=> nrel_goto: .proc_of_searching_for_current_state_of_specified_object_operator_return;;
			*);;

		-> .proc_of_searching_for_current_state_of_specified_object_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;